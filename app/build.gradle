apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'


static def appVersionCode() {
    Process process = "git rev-list --count HEAD".execute()
    process.waitFor()
    return process.getText().toInteger()
}

android {
    compileSdkVersion 31
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.yfbx.hookdemo"
        minSdkVersion 19
        targetSdkVersion 31
        versionCode appVersionCode()
        versionName "1.0.${versionCode}"
    }

    signingConfigs {
        release {
            storeFile file("../hook.keystore")
            keyAlias 'yfbx'
            keyPassword '123456'
            storePassword '123456'
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def type = variant.buildType.name
            outputFileName = "HookDemo_${type}_${versionName}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation "androidx.core:core-ktx:1.7.0"
    implementation 'com.google.android.material:material:1.4.0'

    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'

}
